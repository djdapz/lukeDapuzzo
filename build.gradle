group = 'com.devon.dapuzzo'
version = '0.0.1-SNAPSHOT'

description = """Luke D'Apuzzo's website"""

buildscript {
    ext.kotlin_version = '1.1.4-3'
    repositories {
        mavenCentral()
        maven { url 'http://repo.spring.io/release' }
        maven { url "https://plugins.gradle.org/m2/" }
    }

    dependencies {
        classpath group: 'org.springframework.boot', name: 'spring-boot-gradle-plugin', version: '1.5.2.RELEASE'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.kotlin:kotlin-allopen:$kotlin_version"
        classpath "org.jetbrains.kotlin:kotlin-noarg:$kotlin_version"
    }
}


apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: "kotlin-allopen"
apply plugin: "kotlin-noarg"
apply plugin: 'org.springframework.boot'

allOpen {
    annotation("org.springframework.stereotype.Service")
    annotation("org.springframework.stereotype.Component")
}

//noinspection GroovyUnusedAssignment
sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    compile(
            "com.fasterxml.jackson.core:jackson-core:2.9.2",
            "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.9.2",
            'commons-validator:commons-validator:1.4.0',
            "org.jetbrains.kotlin:kotlin-stdlib-jre8:$kotlin_version",
            "org.flywaydb:flyway-core:4.1.0",
            'org.postgresql:postgresql:9.4.1212.jre7',
            'org.springframework.boot:spring-boot-starter-thymeleaf:1.5.1.RELEASE',
            'org.springframework.boot:spring-boot-starter-data-jpa:1.5.1.RELEASE',
            'org.springframework.boot:spring-boot-starter-mail:1.4.3.RELEASE',
            "com.fasterxml.jackson.module:jackson-module-kotlin:2.9.0"
    )

    runtime group: 'com.h2database', name: 'h2', version: '1.4.193'

    testCompile "com.nhaarman:mockito-kotlin-kt1.1:1.5.0"
    testCompile "io.rest-assured:rest-assured:3.0.5"
    testCompile(group: 'org.springframework.boot', name: 'spring-boot-starter-test', version: '1.5.1.RELEASE') {
        exclude(module: 'commons-logging')
    }
}

//HEROKU SPECIFIC TASKS
task webpack(type: Exec) {
    commandLine "npm", "run", "build"
}

task npmInstall(type: Exec) {
    commandLine "npm", "install"
}

processResources {
    dependsOn webpack
}

webpack {
    dependsOn npmInstall
}

assemble {
    dependsOn webpack
}

bootRun {
    //noinspection GroovyAssignabilityCheck
    systemProperties System.properties
}